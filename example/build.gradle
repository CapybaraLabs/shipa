plugins {
	id "org.jetbrains.kotlin.jvm"
	id "org.springframework.boot"
	id "org.jetbrains.kotlin.plugin.spring" // opens certain classes
	id "com.adarshr.test-logger"
	id "jacoco"
}

configurations {
	//undertow master race
	implementation.exclude module: "spring-boot-starter-reactor-netty"
	implementation.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
	implementation platform(libs.kotlin.bom)
	implementation platform(libs.spring.boot.bom)
	developmentOnly platform(libs.spring.boot.bom)
	testImplementation platform(libs.spring.boot.bom)
	testRuntimeOnly platform(libs.spring.boot.bom)

	implementation rootProject
	implementation "org.springframework.boot:spring-boot-starter-undertow"
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	developmentOnly "org.springframework.boot:spring-boot-devtools"
	implementation "org.apache.httpcomponents.client5:httpclient5"

	testImplementation "org.junit.jupiter:junit-jupiter-api"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation libs.mockitokotlin
	testImplementation "com.jayway.jsonpath:json-path-assert"
}

test {
	useJUnitPlatform()
	jvmArgs("-XX:TieredStopAtLevel=1")
}

testlogger {
	theme = "mocha"
}


springBoot {
	mainClass = "dev.capybaralabs.shipa.LauncherKt"
}

jar {
	onlyIf { false }
}


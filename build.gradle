plugins {
	// Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
	id "org.jetbrains.kotlin.jvm" version "1.7.0"
	id "com.github.ben-manes.versions" version "0.42.0"
	id "org.springframework.boot" version "2.7.1"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
		vendor = JvmVendorSpec.ADOPTIUM
	}
}

repositories {
	// Use Maven Central for resolving dependencies.
	mavenCentral()
}

dependencies {
	implementation platform("org.jetbrains.kotlin:kotlin-bom:1.7.0")
	implementation platform("org.springframework.boot:spring-boot-dependencies:2.7.1")

	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	implementation "org.springframework.boot:spring-boot-starter"
}

springBoot {
	mainClass = "dev.capybaralabs.shipa.LauncherKt"
}

def isNonStable = { String version ->
	def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
	def regex = /^[0-9,.v-]+(-r)?$/
	return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
	rejectVersionIf {
		isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
	}
}
tasks.named("dependencyUpdates").configure {
	checkConstraints = true
	checkBuildEnvironmentConstraints = true
	gradleReleaseChannel = "current"
}


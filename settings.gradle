pluginManagement {
	repositories {
		gradlePluginPortal()
		mavenCentral()
		maven { url "https://repo.spring.io/milestone" }
	}

	plugins {
		id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
		id "org.jetbrains.kotlin.kapt" version "$kotlinVersion"
		id "com.github.ben-manes.versions" version "$versionsVersion"
		id "org.springframework.boot" version "$springBootVersion"
		id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
		id "com.adarshr.test-logger" version "$testloggerVersion"
		id "org.sonarqube" version "$sonarVersion"
	}
}


rootProject.name = "shipa"
include "example"

dependencyResolutionManagement {

	versionCatalogs {
		libs {
			version("spring-boot", "$springBootVersion")
			version("kotlin", "$kotlinVersion")

			library("kotlin-bom", "org.jetbrains.kotlin", "kotlin-bom").versionRef("kotlin")
			library("spring-boot-bom", "org.springframework.boot", "spring-boot-dependencies").versionRef("spring-boot")
			library("sentry-bom", "io.sentry", "sentry-bom").version("$sentryVersion")
			library("saltycoffee", "software.pando.crypto", "salty-coffee").version("$saltyCoffeeVersion")
			library("mockitokotlin", "org.mockito.kotlin", "mockito-kotlin").version("$mockitoKotlinVersion")
		}
	}
}
